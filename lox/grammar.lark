?start     : program

program    : stmt*

?stmt      : expr ";"
           | print_cmd
           | block
           // | if_cmd
           // | for_cmd
           // | ...

print_cmd  : "print" expr ";"

block      : "{" stmt* "}"


// EXPRESSÃ•ES

?expr      : assignment

?assignment: VAR "=" assignment   -> assign
           | or_

?or_       : and_
           | or_ "or" and_   -> or_

?and_      : test
           | and_ "and" test -> and_

?test      : test "==" cmp  -> eq
           | test "!=" cmp  -> ne
           | cmp

?cmp       : cmp ">" factor   -> gt
           | cmp "<" factor   -> lt
           | cmp ">=" factor  -> ge
           | cmp "<=" factor  -> le
           | factor

?factor    : factor "+" term  -> add
           | factor "-" term  -> sub
           | term

?term      : term "*" unary   -> mul
          | term "/" unary   -> div
          | unary

?unary     : "!" unary        -> not_
          | "-" unary        -> neg
          | atom

?atom      : call

call       : primary call_suffix*

call_suffix: "(" params ")"   -> args
           | "." VAR          -> attr

?primary   : NUMBER
           | BOOL
           | VAR
           | STRING
           | NIL
           | "(" expr ")"

params     : [ expr ("," expr )* ]

BOOL.2     : "true" | "false"
VAR.1      : /[a-z_]\w*/
NUMBER     : /([1-9][0-9]*|0)(\.[0-9]+)?/ 
STRING     : /"[^"]*"/
NIL.2      : "nil"
COMMENT    : "//" /[^\n]*/

%ignore /\s/ | COMMENT